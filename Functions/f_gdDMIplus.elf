{*******************************************************************
Description: Directional Movement Index Plus
Provided By: Giulio D'Urso (c) Copyright 2009
********************************************************************}

Inputs: Length(NumericSimple);
Variables: Counter(0), TRange(0), MyRange(0), PlusDM14(0), PlusDM(0), MinusDM(0);

If CurrentBar = 1 Or Length <> MyRange Then Begin
	MyRange = Length;
	gdDMIplus = 0;
	PlusDM14 = 0;
	TRange = 0;
	For Counter = 0 To MyRange - 1 Begin
		If High[Counter] - High[Counter+1] < 0 Then 
			PlusDM = 0 
		Else 
			PlusDM = High[Counter] - High[Counter+1];
		If Low[Counter+1] - Low[Counter] < 0 Then 
			MinusDM = 0 
		Else 
			MinusDM = Low[Counter+1] - Low[Counter];
		If MinusDM >= PlusDM Then 
			PlusDM = 0;  
		{MinusDM not set to 0 because it is not used}
		TRange = TRange + TrueRange[Counter];
		PlusDM14 = PlusDM14 + PlusDM;
	End;
	If TRange <> 0 Then 
		gdDMIplus = 100 * PlusDM14 / TRange
	Else 
		gdDMIplus = 0 ;
End
Else 
	If CurrentBar > 1 Then Begin
		If High[0] - High[1] < 0 Then 
			PlusDM = 0 
		Else 
			PlusDM = High[0] - High[1];
		If Low [1] - Low [0] < 0 Then 
			MinusDM = 0 
		Else 
			MinusDM = Low[1] - Low[0];
		If MinusDM >= PlusDM Then 
			PlusDM = 0;  
		{MinusDM not set to 0 because it is not used}
		If MyRange > 0 Then Begin
			TRange = TRange[1] - (TRange[1] / MyRange) + TrueRange;
			PlusDM14 = PlusDM14[1] - (PlusDM14[1] / MyRange) + PlusDM;
		End;
		If TRange <> 0 Then 
			gdDMIplus = 100 * PlusDM14 / TRange
		Else 
			gdDMIplus = 0 ;
	End ;
